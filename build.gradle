apply plugin: 'kotlin'
apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'idea'

group = 'se.zensum'
version = '1.0-SNAPSHOT'
description = 'description of project'

defaultTasks 'run'

buildscript {
    ext.kotlin_version = '1.1.51'
    ext.jvm_version = '1.8'
    ext.junit_platform_version = '1.0.0-M4'
    repositories {
        mavenCentral()
        maven { url "https://plugins.gradle.org/m2/" }
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "org.junit.platform:junit-platform-gradle-plugin:$junit_platform_version"
        classpath 'com.github.jengelman.gradle.plugins:shadow:2.0.0'
    }
}

task run(type: JavaExec) {
    main = 'franz.MainKt' //Important that 'Kt' is appended to class name
    classpath = sourceSets.main.runtimeClasspath
}

apply plugin: "org.junit.platform.gradle.plugin"
apply plugin: 'com.github.johnrengelman.shadow'

dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib-jre8:$kotlin_version"
    compile group: 'org.jetbrains.kotlinx', name: 'kotlinx-coroutines-jdk8', version: '0.19.1'
    compile group: 'org.jetbrains.kotlinx', name: 'kotlinx-coroutines-core', version: '0.19.1'
    compile 'io.github.microutils:kotlin-logging:1.4.6'
    compile 'org.apache.kafka:kafka-clients:0.11.0.0'
    compile group: 'org.xeustechnologies', name: 'jcl-core', version: '2.8'
    runtime 'ch.qos.logback:logback-classic:1.2.3'
    testCompile 'org.junit.jupiter:junit-jupiter-api:5.0.0-M4'
    testCompile "org.jetbrains.kotlin:kotlin-test-junit:$kotlin_version"
    testRuntime 'org.junit.jupiter:junit-jupiter-engine:5.0.0-M4'
}

junitPlatform {
    platformVersion = junit_platform_version
}

jar {
    manifest {
        attributes 'Main-Class': "franz.MainKt"
    }
}

shadowJar {
    baseName = 'shadow'
    classifier = null
    version = null
}

repositories {
    maven { url "https://dl.bintray.com/kotlin/ktor" }
    maven { url "http://jcenter.bintray.com" }
}

tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).all {
    sourceCompatibility = jvm_version
    kotlinOptions {
        jvmTarget = jvm_version
    }
}

compileJava {
    sourceCompatibility = jvm_version
    targetCompatibility = jvm_version
    options.incremental = true
    options.encoding = 'UTF-8'
}

kotlin {
    experimental {
        coroutines 'enable'
    }
}

// Important: All classes containing test cases must match the
// the regex pattern "^.*Tests?$" to be picked up by the junit-gradle plugin.
sourceSets {
    main.resources.srcDirs += 'src/main/resources'
    main.kotlin.srcDirs += 'src/main/kotlin'
    test.kotlin.srcDirs += 'src/test/kotlin'
    main.java.srcDirs += 'src/main/java'
    test.java.srcDirs += 'src/test/java'
}

task wrapper(type: Wrapper) {
    description = 'Generates gradlew[.bat] scripts for faster execution'
    gradleVersion = '4.2'
}
